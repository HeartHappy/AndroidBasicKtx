plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}
group("com.hearthappy")
version(libraryVersion)

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}


task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set("sources")
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.hearthappy'
                artifactId = 'android-basic-ktx'
                version = libraryVersion
                afterEvaluate {
                    from components.release
                }
//                artifact("$buildDir/outputs/aar/android-basic-ktx-release-${libraryVersion}.aar")
//                artifacts {
//                    archives(javadocJar)
//                    archives(sourceJar)
//                }
                pom {
                    name = 'android-basic-ktx'
                    description = 'Android basic framework packaging and extension library'
                    url = 'https://github.com/HeartHappy/AndroidBasicKtx'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'HeartHappy'
                            name = 'এ᭄白ຼ龙້໌ᮨꦿ'
                            email = '1151087058@qq.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:https://github.com/HeartHappy/AndroidBasicKtx.git'
                        developerConnection = 'scm:git:ssh://github.com/HeartHappy/AndroidBasicKtx.git'
                        url = 'https://github.com/HeartHappy/AndroidBasicKtx'
                    }
                }
            }
        }
        repositories {
            maven {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = project.properties['sonatypeUsername']
                    password = project.properties['sonatypePassword']
                }
            }
        }
    }
}

android {
    namespace 'com.hearthappy.androidbasicktx'
    compileSdk 31

    defaultConfig {
        minSdk 21
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
    libraryVariants.all { variant ->
        variant.outputs.all {
            // 定义新的文件名，这里以库名、版本名和构建类型为例
            def newName = "android-basic-ktx-${variant.buildType.name}-${libraryVersion}.aar"
            outputFileName = newName
        }
    }
}

dependencies {

    implementation libs.androidx.core.ktx
//    implementation platform(libs.kotlin.bom)
    implementation libs.androidx.appcompat
    implementation libs.material
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
}